apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

ext {
    support_version = "25.4.0"
    firebase_version = "9.2.0"
    guava_version = "20.0"
}

android {
    compileSdkVersion 25
    buildToolsVersion "26.0.2"
    defaultConfig {
        applicationId "com.kikatech.go"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 180008010
        versionName "0.8.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a"
        }
    }

    signingConfigs {
        release {
            storeFile file('debug.keystore')
            keyAlias 'debug'
            keyPassword 'android'
            storePassword 'android'
        }
        debug {
            storeFile file('debug.keystore')
            keyAlias 'debug'
            keyPassword 'android'
            storePassword 'android'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        checkReleaseBuilds false // Temporary change for some build fail issue.
        abortOnError false // Temporary change for some build fail issue.
        disable 'MissingTranslation', 'InvalidPackage', 'InflateParams', 'ContentDescription', 'GradleCompatible'
    }

    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a'
            universalApk false
        }
    }

    flavorDimensions "tier"
    productFlavors {
        main {
            flavorDimensions "tier"
        }
        manufacturer {
            flavorDimensions "tier"
        }
    }

    dataBinding {
        enabled = true
    }

    configurations.all {
        resolutionStrategy {
            force "com.google.guava:guava:$guava_version"
            force "com.google.code.findbugs:jsr305:2.0.1"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    annotationProcessor 'com.bluelinelabs:logansquare-compiler:1.3.7'
    testImplementation 'junit:junit:4.12'
    // Android Support
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:recyclerview-v7:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation 'com.android.support:multidex:1.0.3'
    // Guava
    implementation "com.google.guava:guava:$guava_version"
    // Google Play Services
    implementation 'com.google.android.gms:play-services-location:9.2.0'
    // Fabric
    implementation('com.crashlytics.sdk.android:crashlytics:2.8.0@aar') {
        transitive = true
    }
    implementation('com.crashlytics.sdk.android:crashlytics-ndk:2.0.1@aar') {
        transitive = true
    }
    // Api.ai
    implementation 'ai.api:libai:1.6.11'
    implementation 'ai.api:sdk:2.0.7@aar'
    // Gson
    implementation 'com.google.code.gson:gson:2.8.1'
    // Glide
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'jp.wasabeef:glide-transformations:2.0.1'
    // EventBus
    implementation 'org.greenrobot:eventbus:3.1.1'
    // Emoji
    implementation 'com.vdurmont:emoji-java:4.0.0'
    // Youtube Extractor
    implementation 'com.github.HaarigerHarald:android-youtubeExtractor:master-SNAPSHOT'
    // Youtube Data API v3
    implementation 'com.google.apis:google-api-services-youtube:v3-rev189-1.23.0'
    // Amazon Web Services
    implementation 'com.amazonaws:aws-android-sdk-s3:2.3.3'
    implementation 'com.amazonaws:aws-android-sdk-polly:2.3.4'
    // Firebase
    implementation "com.google.firebase:firebase-core:$firebase_version"
    implementation "com.google.firebase:firebase-config:$firebase_version"

    implementation project(':usb')
//    implementation 'com.kikatech.voice:voice:1.0.7.5'
//    implementation 'com.kikatech.voice:wakeup:1.0.7.5'
    implementation project(':voiceSDK')
    implementation project(':wakeup')
}

crashlytics {
    enableNdk true
    androidNdkOut 'obj'
    androidNdkLibsOut 'jniLibs'
}

apply plugin: 'com.google.gms.google-services'
