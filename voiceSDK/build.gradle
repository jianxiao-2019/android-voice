apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 25
    buildToolsVersion "26.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 10103
        versionName "1.1.03_20171013"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }
}

def getVersionName(String versionName) {
    if (isRelease()) {
        if (versionName.endsWith('-SNAPSHOT')) {
            versionName = versionName.replace("-SNAPSHOT", "")
        }
    } else {
        if (!versionName.endsWith('-SNAPSHOT')) {
            versionName = versionName + "-SNAPSHOT"
        }
    }
    println 'voice SDK VersionName=' + versionName;
    return versionName
}

def getVersionCode(int versionCode) {
    if (hasProperty("VersionCode")) {
        return Integer.parseInt(getProperty("VersionCode").toString())
    }
    return versionCode
}

def isRelease() {
    if (hasProperty('RELEASE')) {
        return getProperty('RELEASE').toBoolean()
    }
    return false
}

apply from: 'gradle-mvn-push.gradle'

ext {
    // supportLibraryVersion = '25.4.0'
    grpcVersion = '1.4.0'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.3.0'
    }
    plugins {
        javalite {
            artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0"
        }
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                javalite {}
                grpc {
                    // Options added to --grpc_out
                    option 'lite'
                }
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    annotationProcessor 'com.bluelinelabs:logansquare-compiler:1.3.7'
    compile 'com.bluelinelabs:logansquare:1.3.7'
    compile 'com.android.support:appcompat-v7:25.4.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.kikatech.androidspeex:speex:1.5'
    compile 'org.java-websocket:Java-WebSocket:1.3.4'
    compile 'org.greenrobot:eventbus:3.1.1'
    // Api.ai
    compile 'ai.api:libai:1.6.11'
    compile 'ai.api:sdk:2.0.7@aar'
    compile 'me.xdrop:fuzzywuzzy:1.1.9'

    // gRPC
    compile "io.grpc:grpc-okhttp:$grpcVersion"
    compile "io.grpc:grpc-protobuf-lite:$grpcVersion"
    compile "io.grpc:grpc-stub:$grpcVersion"
    compile 'javax.annotation:javax.annotation-api:1.2'
    protobuf 'com.google.protobuf:protobuf-java:3.3.1'

    // OAuth2 for Google API
//    compile 'com.google.cloud:google-cloud-speech:0.46.0-alpha'
//    compile 'com.google.api.grpc:grpc-google-cloud-speech-v1:0.0.7';
    compile('com.google.auth:google-auth-library-oauth2-http:0.7.0') {
        exclude module: 'httpclient'
    }

    testCompile 'junit:junit:4.12'
}

android {
    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a'
            universalApk false
        }
    }
}
